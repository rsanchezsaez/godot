#!/usr/bin/env python
from misc.utility.scons_hints import *

import platform_ios_builders

Import("env")

ios_lib = [
    "device_metrics.mm",
    "display_layer.mm",
    "display_server.mm",
    "godot_view.mm",
    "ios.mm",
    "main.mm",
    "os_ios.mm",
]

env_ios = env.Clone()
ios_lib = env_ios.add_library("ios", ios_lib)

# (iOS) Enable module support
env_ios.Append(CCFLAGS=["-fmodules", "-fcxx-modules"])


combine_command = env_ios.CommandNoCache(
    "#bin/libgodot" + env_ios["LIBSUFFIX"], [ios_lib] + env_ios["LIBS"], env.Run(platform_ios_builders.combine_libs)
)

if env["generate_bundle"]:
    env.AlwaysBuild(
        env.CommandNoCache("generate_bundle", combine_command, env.Run(platform_ios_builders.generate_bundle))
    )
